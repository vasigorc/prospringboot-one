buildscript {
    ext {
        springBootVersion = '1.3.6.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

apply plugin: 'java'
apply plugin: 'java-library-distribution'
apply plugin: 'eclipse'
apply plugin: 'spring-boot'

distributions {
    main{
        baseName = 'boot-one'
    }
}

if (!hasProperty('mainClass')) {
    ext.mainClass = 'src.main.java.com.vgorcinschi.bootone.main.BootOneApplication'
}

task wrapper(type: Wrapper){
    gradleVersion='2.14'
}

test.testLogging.showStandardStreams = true

jar {
    baseName = 'boot-one'
    version = '0.0.1-SNAPSHOT'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}


dependencies {
    compile('org.springframework.boot:spring-boot-starter-data-jpa')
    compile('org.springframework.boot:spring-boot-starter-thymeleaf')
    compile('org.springframework.boot:spring-boot-starter-web')
    runtime('com.h2database:h2')
    testCompile('org.springframework.boot:spring-boot-starter-test')
}


eclipse {
    classpath {
        containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
        containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
    }
}

ext.versionFile = file('version.properties')

task loadVersion {
    project.version = readVersion()
}

ProjectVersion readVersion(){
    if(!versionFile.exists()){
        throw new GradleException("Required version file does not exist:\n\
$versionFile.canonicalPath")
    }
    
    Properties versionProps = new Properties()
    
    versionFile.withInputStream {
        stream -> versionProps.load(stream)
    }
    
    new ProjectVersion(versionProps.major.toInteger(), versionProps.minor.toInteger(),
        versionProps.release.toBoolean())
}

class ProjectVersion {
    Integer major
    Integer minor
    Boolean release
    
    ProjectVersion(Integer major, Integer minor){
        this.major=major
        this.minor=minor
        this.release=Boolean.FALSE
    }
    
    ProjectVersion(Integer major, Integer minor, Boolean release){
        this.major=major
        this.minor=minor
        this.release=release
    }
    
    @Override
    String toString(){
        "$major.$minor${release ? '' : '-SNAPSHOT'}"
    }
}

task printVersion << {
    logger.quiet "Version: $version"
}

task incrementMajorVersion(group: 'versioning', description: 'Increments project \n\
major version.') << {
    String currentVersion = version.toString()
    ++version.major
    String newVersion = version.toString()
    logger.info "Increment major project version: $currentVersion -> $newVersion"
    
    ant.propertyfile(file: versionFile) {
        entry(key: 'major', type: 'int', operation: '+', value: 1)
    }
}

task incrementMinorVersion(group: 'versioning', description: 'Increments project \n\
minor version.') << {
    String currentVersion = version.toString()
    ++version.minor
    String newVersion = version.toString()
    logger.info "Increment minor project version: $currentVersion -> $newVersion"
    
    ant.propertyfile(file: versionFile) {
        entry(key: 'minor', type: 'int', operation: '+', value: 1)
    }
}
